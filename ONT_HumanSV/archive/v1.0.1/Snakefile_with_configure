##-----------------------------------------------------------------------------
## NAMES: Structual Variants Detection Pipeline
## BUILD: Snakemake 6.4.1
## CONDAE_ENV: biosoft_py37
## VERSION: 1.0
## DATE: 2021.6.11
##-----------------------------------------------------------------------------

#### Configure area ####
#~ Last step, move to config.yml
REFERENCE_38 = "/nfs-test/mxf/Database/references/homo_sapiens/hg38.fa"
MINIMAP2_INDEX = REFERENCE_38.replace("fa", "mmi")
UCSC_DB_DIR = "/nfs-test/mxf/Database/UCSC/hg38"
THREADS=16
INPUT_FASTQ = "data/test.fastq.gz"
TARGET_INTERVAL = ""
WORKDIR = "results/20210621"
SAMPLE_NAME = "test"
########################

rule all:
    input:
        # expand("{workdir}/{sample}.merged_3callers_filt.vcf", workdir=WORKDIR, sample=SAMPLE_NAME)
        expand("{workdir}/{sample}.merged_3callers_filt.hg38_annotsv.tsv", workdir=WORKDIR, sample=SAMPLE_NAME)

rule make_minimap2_index:
    input:
        REFERENCE_38
    output:
        MINIMAP2_INDEX
    params:
        "-ax map-ont --MD -Y"
    shell:
        "minimap2 {params} {input} -d {output}"
            
rule minimap2_alignment:
    input:
        MINIMAP2_INDEX,
        INPUT_FASTQ
    output:
        "{workdir}/{sample}.minimap2.bam"
    log:
        "{workdir}/log/{sample}.minimap2_alignment.log"
    benchmark:
        "{workdir}/benchmark/{sample}.minimap2_alignment.tsv"
    threads:
        THREADS
    params:
        minimap2_align = "-ax map-ont --MD -Y",
        samtools_sort = "-O BAM"
    shell:
        "minimap2 -t {threads} {params.minimap2_align} {input} | \
        samtools sort -@ {threads} {params.samtools_sort} -o {output} - && \
        samtools index -@ {threads} {output} 2> {log}"
        
rule bam2bed:
    input:
        "{workdir}/{sample}.minimap2.bam"
    output:
        "{workdir}/{sample}.minimap2.bed"
    log:
        "{workdir}/log/{sample}.bam2bed.log"
    benchmark:
        "{workdir}/benchmark/{sample}.bam2bed.tsv"
    run:
        if TARGET_INTERVAL == "":
            shell("bedtools bamtobed -i {input} | \
                bedtools merge -i - | \
                awk '$1!~/_random|chrUn_/' > {output} 2> {log}")
        else:
            shell("cp {TARGET_INTERVAL} {output}")
            
rule run_sniffles:
    input:
        "{workdir}/{sample}.minimap2.bam"
    output:
        "{workdir}/{sample}.sniffles.vcf"
    log:
        "{workdir}/log/{sample}.sniffles.log"
    benchmark:
        "{workdir}/benchmark/{sample}.sniffles.tsv"
    threads:
        THREADS
    params:
        "--min_support 1 --min_length 50 --num_reads_report -1 --min_seq_size 500 --genotype"
    shell:
        "sniffles -t {threads} {params} -m {input} -v {output} 2> {log}"
        
rule run_cuteSV:
    input:
        "{workdir}/{sample}.minimap2.bam",
        REFERENCE_38
    output:
        "{workdir}/{sample}.cutesv.vcf",
        directory("{workdir}/{sample}_cutesv_calls")
    log:
        "{workdir}/log/{sample}.cutesv.log"
    benchmark:
        "{workdir}/benchmark/{sample}.cutesv.tsv"
    threads:
        THREADS
    params:
        "--min_size 50 --max_size 1000000 --retain_work_dir \
        --report_readid --min_support 1 --genotype \
        --max_cluster_bias_INS 100 --diff_ratio_merging_INS 0.3 \
        --max_cluster_bias_DEL 100 --diff_ratio_merging_DEL 0.3"
    shell:
        "mkdir {output[1]}\
        && cuteSV -t {threads} {params} {input} {output} 2> {log}"

rule run_svim:
    input:
        "{workdir}/{sample}.minimap2.bam",
        REFERENCE_38
    output:
        directory("{workdir}/{sample}_svim_calls"),
        "{workdir}/{sample}_svim_calls/variants.vcf"
    log:
        "{workdir}/log/{sample}.svim.log"
    benchmark:
        "{workdir}/benchmark/{sample}.svim.tsv"
    params:
        "--min_sv_size 50 --max_sv_size 1000000 --minimum_depth 1 --read_names --sequence_alleles"
    shell:
        "svim alignment {params} {output[0]} {input} 2> {log}"
        
rule merge_SVs:
    input:
        "{workdir}/{sample}.sniffles.vcf",
        "{workdir}/{sample}.cutesv.vcf",
        "{workdir}/{sample}_svim_calls/variants.vcf"
    output:
        "{workdir}/{sample}.merged_1callers.vcf",
        "{workdir}/{sample}.merged_2callers.vcf",
        "{workdir}/{sample}.merged_3callers.vcf",
        vcf_names = "{workdir}/{sample}_vcf_names"
    log:
        "{workdir}/log/{sample}.merge_SVs.log"
    benchmark:
        "{workdir}/benchmark/{sample}.merge_SVs.tsv"
    params:
        m1 = "1000 1 1 1 0 50",
        m2 = "1000 2 1 1 0 50",
        m3 = "1000 3 1 1 0 50"
    ## Merge SVs
    #~ Minimum number of supporting caller 1 2 3
    shell:
        "ls {input} > {output.vcf_names} \
        && SURVIVOR merge {output.vcf_names} {params.m1} {output[0]} 2>> {log} \
        && SURVIVOR merge {output.vcf_names} {params.m2} {output[1]} 2>> {log} \
        && SURVIVOR merge {output.vcf_names} {params.m3} {output[2]} 2>> {log}"

rule filter_by_region:
    input:
        "{workdir}/{sample}.merged_3callers.vcf",
        interval = "{workdir}/{sample}.minimap2.bed"
    output:
        "{workdir}/{sample}.merged_3callers_filt.vcf"
    log:
        "{workdir}/log/{sample}.filter_by_region.log"
    benchmark:
        "{workdir}/benchmark/{sample}.filter_by_region.tsv"
    ## [20210621] bedtools [sort -> merge] eliminate redundancy
    params:
        gap = "-header -f 0.5 -v -b " + UCSC_DB_DIR + "/UCSC_gap_non_redundancy.hg38.bed",
        centro = "-header -f 0.5 -v -b " + UCSC_DB_DIR + "/UCSC_centromeres_non_redundancy.hg38.bed",
        telo = "-header -f 0.5 -v -b " + UCSC_DB_DIR + "/UCSC_telomeres_non_redundancy.hg38.bed",
        seg_dup = "-header -f 0.5 -v -b " + UCSC_DB_DIR + "/UCSC_segmental_dups_non_redundancy.hg38.bed",
        trf = "-header -f 0.5 -v -b " + UCSC_DB_DIR + "/UCSC_single_repeats_non_redundancy.hg38.bed"
    ## filter by region
    ## [1] retaion target region
    ## [2] discard genome special region opverlap 50%: 
    ## gap, centromeres, telomeres, segmental duplications, tanden repeats
    shell:
        "grep -v -E '_random|chrUn_' {input[0]} | \
        bedtools intersect -header -a - -b {input.interval} | \
        bedtools intersect {params.gap} -a - | \
        bedtools intersect {params.centro} -a - | \
        bedtools intersect {params.telo} -a - | \
        bedtools intersect {params.seg_dup} -a - | \
        bedtools intersect {params.trf} -a - > {output} 2> {log}"
        
rule run_annotsv:
    input:
        "{workdir}/{sample}.merged_3callers_filt.vcf"
    output:
        "{workdir}/{sample}.merged_3callers_filt.hg38_annotsv.tsv"
    log:
        "{workdir}/log/{sample}.run_annotsv.log"
    benchmark:
        "{workdir}/benchmark/{sample}.run_annotsv.tsv"
    params:
        "-genomeBuild GRCh38 -SVinputInfo 0 -annotationMode full"
    ## AnnotSV path need export to environment variants
    ## see: https://github.com/lgmgeo/AnnotSV
    shell:
        "$ANNOTSV/bin/AnnotSV {params} -SVinputFile {input} -outputFile {output} 2> {log}"
